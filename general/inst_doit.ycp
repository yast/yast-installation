/**
 *
 * Author:	Klaus Kämpf <kkaempf@suse.de>
 *		Stefan Hundhammer <sh@suse.de>
 *		Arvin Schnell <arvin@suse.de>
 *
 * Purpose:	This module ask about do the installation or not,
 *
 * $Id$
 */
{
    textdomain "installation";

    import "Wizard";
    import "Mode";
    import "AutoInstall";

    import "Label";

    if (Mode::autoinst)
    {
	if (!AutoInstall::Confirm)
	{
	    Wizard::SetEasyInstallationStep( EasyInstallationStepDescriptions(), 3 );
	    return `next;
	}
    }

    if ( WFM::Args(2) )	// going backwards?
    {
	// This module can only be reached in backward direction if any of the
	// subsequent modules failed - creating the partitions, formatting etc.
	//
	// None of the subsequent interactive dialogs are allowed to have a
	// `next button enabled, so there is no chance of getting here manually.

	return `back;
    }


    /**
     * Returns a term containing the final installation confirmation dialog.
     * @return confirmation dialog as term
     **/
    define term confirmInstallationDialog() ``{

	string confirmation_message = "";
	string confirmation_button = "";

	if (Mode::installation)
	{
	    // Confirmation message:
	    //
	    // Ask user if he really wishes to begin the installation, i.e.
	    // format hard disk partitions and install software. Before this
	    // point nothing has been changed - now we really mean business.
	    //
	    // `Yes' will prepare (format) the disk and install the software.
	    // `No' will go back; further config steps are possible.
	    // Translators notice: Line length < 48 chars.
	    confirmation_message = _("Warning:
YaST2 has obtained all the information
required to install SuSE Linux.
The installation will be carried out according
to settings made in the previous dialogs.
To commit the installation and all choices made
so far, choose \"Yes\".  Choose \"No\" to return
to the previous dialog.

Start installation?
");

	    // PushButton: Allright, really start installation
	    confirmation_button = _("&Yes, install");
	}

	if (Mode::update)
	{
	    // Confirmation message:
	    //
	    // Ask user if he really wishes to begin the update, i.e. update
	    // software. Before this point nothing has been changed - now we
	    // really mean business.
	    //
	    // `Yes' will update the software.
	    // `No' will go back; further config steps are possible.
	    // Translators notice: Line length < 48 chars.
	    confirmation_message = _("Warning:
YaST2 has all the data required for updating SuSE Linux.
The update will be carried out according to your
settings made in the previous dialogs.
To commit the update and all choices made
so far, choose \"Yes\".  Choose \"No\" to return
to the previous dialog.

Start update?
");

	    // PushButton: Allright, really start update
	    confirmation_button = _("&Yes, update");
	}

	term dialog = `HBox(
			    `HSpacing(1),
			    `VBox(
				  `VSpacing(0.2),

				  `Heading( confirmation_message ),
				  `HBox(
					`PushButton(`id(`yes), confirmation_button),
					`HStretch(),
					`PushButton(`id(`no), `opt(`default), Label::NoButton() )
					),
				  `VSpacing(0.2)
				  ),
			    `HSpacing(1)
			    );

	return dialog;
    };


    //
    // main()
    //

    UI::OpenDialog(`opt(`decorated, `warncolor), confirmInstallationDialog() );

    boolean confirmed = false;

    while (true)
    {
	symbol button = UI::UserInput();

	confirmed = button == `yes;

	if ( button == `yes || button == `no )
	    break;
    }

    UI::CloseDialog();

    if ( confirmed )
    {
	Wizard::SetEasyInstallationStep( EasyInstallationStepDescriptions(), 3 );
    }

    return confirmed ? `next : `back;
}
