/**
 *
 * Authors:	Klaus KÃ¤mpf <kkaempf@suse.de>
 *		Stefan Hundhammer <sh@suse.de>
 *		Arvin Schnell <arvin@suse.de>
 *
 * Purpose:	Asks user to really do the installation/update.
 *
 * $Id$
 */
{
    textdomain "installation";

    import "Wizard";
    import "Mode";
    import "AutoinstConfig";

    import "Label";

    if (Mode::autoinst)
    {
	if (!AutoinstConfig::Confirm)
	{
	    return `next;
	}
    }

    if ( (boolean) WFM::Args(2) )	// going backwards?
    {
	// This module can only be reached in backward direction if any of the
	// subsequent modules failed - creating the partitions, formatting etc.
	//
	// None of the subsequent interactive dialogs are allowed to have a
	// `next button enabled, so there is no chance of getting here manually.

	return `back;
    }

    define void ConfirmLicenses () {
	if (Mode::autoinst)
	{
	    return;
	}
	list<string> to_install = Pkg::GetPackages (`selected, true);
	map<string,string> licenses = Pkg::PkgGetLicensesToConfirm (to_install);
	boolean deny_all = false;
	foreach (string package,  string license, licenses, {
	    if (deny_all)
	    {
		Pkg::PkgTaboo (package);
	    }
	    else
	    {
		term popup = `VBox (
		    `HSpacing (80),
		    // dialog heading, %1 is package name
		    `Heading (sformat (_("Confirm package licence: %1"),
			package)),
		    `HBox (
			`VSpacing (20),
			`RichText (`id (`lic), license)
		    ),
		    `HBox (
			`PushButton (`id (`help), Label::HelpButton ()),
			`HStretch (),
			// push button
			`PushButton (`id (`accept), _("I &Agree")),
			// push button
			`PushButton (`id (`deny), _("I &Disagree")),
			// push button
			`PushButton (`id (`deny_all), _("I D&isagree to All"))
		    )
		);
		UI::OpenDialog (popup);
		symbol ret = nil;
		while (ret == nil)
		{
		    ret = (symbol)UI::UserInput ();
		    if (ret == `help)
		    {
			ret = nil;
			// help text
			string help = _("<p><b><big>License Confirmation</big></b><br>
The package in the headline of the dialog requires an explicit confirmation
of accepting its license.
If you reject the license of the package, the package will not be installed.
<br>
To accept the license of the package, click <b>I Agree</b>.
To reject the licence of the package, click <b>I Disagree</b>.
<br>
To reject installing all packages requiring confirmation of
their licenses, click <b>I Disagree to All</b>.</p>");
			UI::OpenDialog (`HBox (
			    `VSpacing (18),
			    `VBox (
				`HSpacing (70),
				`RichText (help),
				`HBox (
				    `HStretch (),
				    `PushButton (`id (`close), _("&Close")),
				    `HStretch ()
				)
			    )
			));
			UI::UserInput ();
			UI::CloseDialog ();
		    }
		}
		UI::CloseDialog ();
		if (ret != `accept)
		{
		    if (ret == `deny_all)
		    {
			deny_all = true;
		    }
		    Pkg::PkgTaboo (package);
		}
		else
		{
		    Pkg::PkgMarkLicenseConfirmed (package);
		}
	    }
	});
    }

    /**
     * Returns a term containing the final installation confirmation dialog.
     * @return confirmation dialog as term
     **/
    define term confirmInstallationDialog() ``{

	string confirmation_message = "";
	string confirmation_button = "";

	if (Mode::installation)
	{
	    // Confirmation message:
	    //
	    // Ask user if he really wishes to begin the installation, i.e.
	    // format hard disk partitions and install software. Before this
	    // point nothing has been changed - now we really mean business.
	    //
	    // `Yes' will prepare (format) the disk and install the software.
	    // `No' will go back; further config steps are possible.
	    // Translators notice: Line length < 48 chars.
	    confirmation_message = sformat (_("Warning:
YaST2 has obtained all the information
required to install %1.
The installation will be carried out according
to settings made in the previous dialogs.
To commit the installation and all choices made
so far, choose \"Yes\".  Choose \"No\" to return
to the previous dialog.

Start installation?
"), UI::GetProductName ());

	    // PushButton: Allright, really start installation
	    confirmation_button = _("&Yes, install");
	}

	if (Mode::update)
	{
	    // Confirmation message:
	    //
	    // Ask user if he really wishes to begin the update, i.e. update
	    // software. Before this point nothing has been changed - now we
	    // really mean business.
	    //
	    // `Yes' will update the software.
	    // `No' will go back; further config steps are possible.
	    // Translators notice: Line length < 48 chars.
	    confirmation_message = sformat (_("Warning:
YaST2 has all the data required for updating %1.
The update will be carried out according to your
settings made in the previous dialogs.
To commit the update and all choices made
so far, choose \"Yes\".  Choose \"No\" to return
to the previous dialog.

Start update?
"), UI::GetProductName ());

	    // PushButton: Allright, really start update
	    confirmation_button = _("&Yes, Update");
	}

	term dialog = `HBox(
			    `HSpacing(1),
			    `VBox(
				  `VSpacing(0.2),

				  `Heading( confirmation_message ),
				  `HBox(
					`PushButton(`id(`yes), confirmation_button),
					`HStretch(),
					`PushButton(`id(`no), `opt(`default), Label::NoButton() )
					),
				  `VSpacing(0.2)
				  ),
			    `HSpacing(1)
			    );

	return dialog;
    };


    //
    // main()
    //

    ConfirmLicenses ();

    UI::OpenDialog(`opt(`decorated, `warncolor), confirmInstallationDialog() );

    boolean confirmed = false;

    while (true)
    {
	any button = UI::UserInput();

	confirmed = button == `yes;

	if ( button == `yes || button == `no )
	    break;
    }

    UI::CloseDialog();

    return confirmed ? `next : `back;
}
