/**
 * File:	installation/general/inst_relase_notes.ycp
 * Module:	Installation
 * Summary:	Display release notes
 * Authors:	Arvin Schnell <arvin@suse.de>
 *
 * Display release notes.
 */

{
    textdomain "installation";

    import "Wizard";
    import "Language";


    /* filename of release notes */
    string file = "";

    /* release notes */
    string text = "";


    /* function to load release notes */
    define boolean load_release_notes () ``{

	string tmp = "/usr/share/doc/RELEASE_NOTES.%1.html";

	file = sformat (tmp, Language::language);
	text = SCR::Read (.target.string, [file, ""]);
	if (text != "")
	    return true;

	file = sformat (tmp, substring (Language::language, 0, 2));
	text = SCR::Read (.target.string, [file, ""]);
	if (text != "")
	    return true;

	file = sformat (tmp, "en");
	text = SCR::Read (.target.string, [file, ""]);
	if (text != "")
	    return true;

	return false;
    };


    /* caption for dialog "Release Notes" */
    string caption = _("Release Notes");

    load_release_notes ();

    term contents = `VBox (
			   `VSpacing (0.5),
			   `Left (`Label (sformat (_("Content of File %1"), file))),
			   `RichText (`id (`text), text),
			   `VSpacing (0.5)
			   );

    /* help text for dialog "Release Notes" */
    string help = _("Here are the <b>release notes</b> for the installed
Linux system. They provide a brief summary of new features and changes.
");

    Wizard::SetContents (caption, contents, help, true, true);

    // FIXME: richtext eats return key
    // UI::SetFocus (`id (`text));

    any ret = nil;

    repeat {

	ret = Wizard::UserInput();

	if (ret == `abort)
	{
	    if (CallFunction (`inst_confirm_abort (`incomplete)))
		break;
	}
	else if (ret == `help)
	{
	    Wizard::ShowHelp (help);
	}

    } until ( ret == `next || ret == `back );

    return ret;
}
