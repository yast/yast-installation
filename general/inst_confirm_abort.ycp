/**
 *
 * $Id$
 *
 * Module:		inst_confirm_abort.ycp
 *
 * Parameters:
 *	`painless	Aborting now will be painless
 *	`incomplete	Aborting now will lead to an incomplete system
 *	`unusable	Aborting now will leave a wrecked hard disk
 *
 * Author:		Stefan Hundhammer <sh@suse.de>
 *
 * Purpose:		Confirm installation abortion
 *
 *
 * user_settings:
 * ---
 */

{
  textdomain "general";
  string what_will_happen = "";


  if ( Args(0) == `painless )
  {
      // Warning text for aborting an installation before anything is installed
      what_will_happen = UI( _("If you abort the installation now,
SuSE Linux will not be installed.
Your hard disk will remain unchanged.
") );
  }
  else if ( Args(0) == `nothing )
  {
      // nothing will happe
      what_will_happen = UI( _("If you abort the installation now,
your hard disk will remain unchanged.
") );
  }      
  else if ( Args(0) == `incomplete )
  {
      // Warning text for aborting an installation during the install process
      // - After some installation steps have been performed - e.g.
      // disks formatted / some packages already installed
      what_will_happen = UI( _("If you abort the installation now, you will
have an incomplete SuSE Linux system
which may or may not be usable.
You may need to reinstall.") );
  }
  else if ( Args(0) == `unusable )
  {
      // Warning text for aborting an installation during the install process
      // right in the middle of some critical process (e.g. formatting)
      what_will_happen = UI( _("If you abort the installation now,
SuSE Linux will be unusable.
You will need to reinstall.") );
  }
  else if ( Args(0) == `desaster)
  {
      // Warning text for aborting an installation during a partition resizing etc.
      what_will_happen = UI( _("If you abort the installation now,
your hard disk may be corrupted --
all systems on this disk could be unusable.
Reinstallation may be required.
") );
  }
  else
  {
      y2warning("Unknown symbol for what will happen when aborting - please correct in calling module" );
  }


  UI::OpenDialog(
		 `opt(`decorated),
		 `HBox(
		       `HSpacing(1),
		       `VBox(
			     `VSpacing(0.2),
			     `HCenter(
				      `HSquash(
					       `VBox(
						     // Confirm user request to abort installation
						     `Left( `Label( _("Really abort the installation?") ) ),
						     `Left( `Label( what_will_happen ) )
						     )
					       )
				      ),
			     `HBox(
				   // Button that will really abort the installation
				   `PushButton(`id(`really_abort), _("&Abort Installation") ),
				   `HStretch(),

				   // Button that will continue with the installation
				   `PushButton(`id(`continue), `opt(`default), _("&Continue Installation") )
				   ),
			     `VSpacing(0.2)
			     ),
		       `HSpacing(1)
		       )
		 );

  any ret = UI::UserInput();
  UI::CloseDialog();

  return (ret == `really_abort);
}
