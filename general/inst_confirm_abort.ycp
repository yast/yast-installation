/**
 *
 * $Id$
 *
 * Module:		inst_confirm_abort.ycp
 *
 * Parameters:
 *	`painless	Aborting now will be painless
 *	`incomplete	Aborting now will lead to an incomplete system
 *	`unusable	Aborting now will leave a wrecked hard disk
 *
 * Author:		Stefan Hundhammer <sh@suse.de>
 *
 * Purpose:		Confirm installation abortion
 *
 *
 * user_settings:
 * ---
 */

{
  textdomain "installation";
  string what_will_happen = "";

  // Confirm user request to abort installation
  string abort_label = _("Really abort the installation?");
  // Button that will really abort the installation
  string abort_button = _("&Abort Installation");
  // Button that will continue with the installation
  string continue_button = _("&Continue Installation");

  if ( WFM::Args(0) == `painless )
  {
      // Warning text for aborting an installation before anything is installed
      what_will_happen = UI( _("If you abort the installation now,
Linux will not be installed.
Your hard disk will remain unchanged.
") );
  }
  else if ( WFM::Args(0) == `nothing )
  {
      // nothing will happe
      what_will_happen = UI( _("If you abort the installation now,
your hard disk will remain unchanged.
") );
  }
  else if ( WFM::Args(0) == `incomplete )
  {
      // Warning text for aborting an installation during the install process
      // - After some installation steps have been performed - e.g.
      // disks formatted / some packages already installed
      what_will_happen = UI( _("If you abort the installation now, you will
have an incomplete Linux system
which may or may not be usable.
You may need to reinstall.") );
  }
  else if ( WFM::Args(0) == `unusable )
  {
      // Warning text for aborting an installation during the install process
      // right in the middle of some critical process (e.g. formatting)
      what_will_happen = UI( _("If you abort the installation now,
Linux will be unusable.
You will need to reinstall.") );
  }
  else if ( WFM::Args(0) == `desaster)
  {
      // Warning text for aborting an installation during a partition resizing etc.
      what_will_happen = UI( _("If you abort the installation now,
your hard disk may be corrupted --
all systems on this disk could be unusable.
Reinstallation may be required.
") );
  }
  else if ( WFM::Args(0) == `repair )
  {
      // Confirm user request to abort System Repair
      abort_label = _("Really abort YaST System Repair?");
      // Button that will really abort the repair
      abort_button = _("Abort System Repair");
      // Button that will continue with the repair
      continue_button = _("&Continue System Repair");
  }
  else
  {
      y2warning("Unknown symbol for what will happen when aborting - please correct in calling module" );
  }


  UI::OpenDialog(
		 `opt(`decorated),
		 `HBox(
		       `HSpacing(1),
		       `VBox(
			     `VSpacing(0.2),
			     `HCenter(
				      `HSquash(
					       `VBox(
						     `Left( `Label( abort_label  ) ),
						     `Left( `Label( what_will_happen ) )
						     )
					       )
				      ),
			     `HBox(
				   `PushButton(`id(`really_abort), abort_button ),
				   `HStretch(),

				   `PushButton(`id(`continue), `opt(`default), continue_button)
				   ),
			     `VSpacing(0.2)
			     ),
		       `HSpacing(1)
		       )
		 );

  any ret = UI::UserInput();
  UI::CloseDialog();

  return (ret == `really_abort);
}
