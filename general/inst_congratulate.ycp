/**
 * File:	installation/general/inst_congratulate.ycp
 * Module:	Installation
 * Summary:	Display congratulation
 * Authors:	Arvin Schnell <arvin@suse.de>
 *
 * Display a nice congratulation message for the user.
 */

{
    textdomain "installation";

    import "Mode";
    import "Wizard";

    // show button "start control center"?
    boolean show_y2cc_button = true;

    map display = UI::GetDisplayInfo();

    if (!display["TextMode"]:true)
    {
	if (!Pkg::IsProvided ("yast2-control-center"))
	{
	    show_y2cc_button = false;
	}
    }

    integer space = display["TextMode"]:true ? 1 : 3;

    // Check box: Should the YaST2 control center automatically
    // be started after this part of the installation is done?
    // Translators: About 40 characters max,
    // use newlines for longer translations.
    term check_box_start_y2cc = `CheckBox (`id(`start_y2cc),
					   _("&Start YaST Control Center"), false);

    // caption for dialog "Congratulation Dialog"
    string caption = _("Installation Complete");

    // congratulation text 1/4
    string text = _("<p><b>Congratulations!</b></p>") +

    // congratulation text 2/4
	_("<p>The installation of SuSE Linux on your machine is complete.
After <b>Finish</b> you can login to the system at once.</p>") +

    // congratulation text 3/4
    // Translators: If there exists a SuSE web-page for your language
    // change the address accordingly. If in doubt leave the original.
	_("<p>Visit us at www.suse.com!</p>") +

    // congratulation text 4/4
	_("<p>Have a lot of fun!<br>Your SuSE Development Team</p>");

    term contents = `VBox (
			   `VSpacing (space),
			   `HBox (
				  `HSpacing (2*space),
				  `RichText (`opt(`richText), text),
				  `HSpacing (2*space)
				  ),
			   `VSpacing (space),
			   check_box_start_y2cc,
			   `VSpacing (2)
			   );

    // help 1/4 for dialog "Congratulation Dialog"
    string help = _("<p>Your system is ready to run!</p>") +

    // help 2/4 for dialog "Congratulation Dialog"
	_("<p><b>Finish</b> will close the YaST installation and will lead you
to the first login.</p>") +

    // help 3/4 for dialog "Congratulation Dialog"
	_("<p>If you choosed the default graphical desktop KDE, you are
allowed to adjust some KDE-settings accordingly to your hardware. Please
notice our SuSE Welcome Dialogue as well.</p>") +

    // help 4/4 for dialog "Congratulation Dialog"
	_("<p>On demand experts can use the full range of SuSE's configuration
modules to this time. Check <b>Start YaST Control Center</b> and it will start
after <b>Finish</b>. Note: The Control Center doesn't have a back button to
this installation sequence.</p>");

    Wizard::SetContentsButtons (caption, contents, help, BackButtonLabel(),
				FinishButtonLabel());

    any ret = nil;

    repeat {

	ret = Wizard::UserInput();

	if (ret == `abort)
	{
	    if (UI::ReallyAbortPopup (false))
	    {
		Wizard::RestoreNextButton();
		break;
	    }
	}
	else if ( ret == `help )
	{
	    Wizard::ShowHelp (help);
	}

    } until ( ret == `next || ret == `back );

    if (ret == `next && UI::QueryWidget (`id(`start_y2cc), `Value))
    {
	// Create empty /var/lib/YaST2/start_y2cc file to signal the calling script
	// that the YaST2 control center should be started after the installation
	SCR::Write (.target.string, "/var/lib/YaST2/start_y2cc", "");
    }

    return ret;
}
