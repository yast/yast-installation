/**
 * Module: 		inst_sw_update.ycp
 *
 * Authors:		Stefan Schubert <schubi@suse.de>
 *
 * Purpose:
 * Displays software selection screen of update.
 *
 *
 * $Id$
 */

{
    textdomain "update";

    import "Arch";
    import "Mode";
    import "Installation";
    import "Wizard";
    import "Packages";
    import "Update";
    import "Kernel";

    boolean retval = false;

    boolean yast2Found = false;

    if ( !Update::updateEvaluated )
    {
	Update::newKernel = Kernel::kernel_packages[0]:"";

	Update::updateEvaluated = true;

	UI::OpenDialog(`opt(`decorated ),`Label(_("Evaluating update packages. One moment please...")));

	// [ affected, not installed, not deleted ]
	list brokenPackages = Pkg::PkgUpdateAll();

	// store all those packages to provide the list for inst_sw_single.ycp
	Update::affected_count = brokenPackages[0]:0;
	Update::manuall_update_list = brokenPackages[1]:[];
	Update::unmaintained_list = brokenPackages[2]:[];

	map update_inf = Pkg::TargetUpdateInf (Installation::destdir + "/var/lib/YaST/update.inf");

	Update::installedVersion = update_inf["distname"]:"?" + " " + update_inf["distversion"]:"";

	integer number_of_sources = size (Packages::theSources);
	if (number_of_sources <= 0)
	{
	    y2error ("No source");
	    Update::updateVersion = "?";
	    Update::updateBasePackages = false;
	}
	else
	{
	    map product = Pkg::SourceProduct (Packages::theSources[number_of_sources-1]:0);
	    y2milestone ("First source product %1", product);
	    if (product == nil) product = $[];
	    Update::updateVersion = product["product"]:"?";
	    string product_package = product["requires"]:"";
	    boolean requires_selected = Pkg::IsSelected (product_package);
	    if (requires_selected == nil) requires_selected = false;
	    if (!requires_selected
		&& !Pkg::IsProvided (product_package))
	    {
		Pkg::DoProvide (product_package);
		requires_selected = true;
	    }
	    Update::updateBasePackages = requires_selected;
	}

	// do not delete "aps" if it is in use
	if (SCR::Execute(.target.bash, sformat("grep -q apsfilter %1/etc/printcap", Installation::destdir)) == 0)
	{
	    Pkg::DoProvide ("aps");
	}
		
	// adding vnc package if in vnc update mode
	if ( Mode::vnc )
	{
	    Pkg::DoProvide ("vnc");
	}

	UI::CloseDialog();

    } // evaluate updatelist


    if ( Mode::test )
    {
	// we have no root-permissions for /var/lib/YaST
	Update::installedVersion = "SuSE-Linux-full-i386-6.4.0#0";
    }

    integer automatikUpdate = Update::affected_count;
    integer manuellUpdate = size ( Update::manuall_update_list );

    // screen title for software selection
    string title = _("Software Update");

    string labelInstalledVersion   = Update::installedVersion;
    string labelUpdateVersion      = Update::updateVersion;
    string labelUpdateBasePackages = "";

    if ( Update::updateBasePackages )
    {
	labelUpdateBasePackages = _("yes");
    }
    else
    {
	labelUpdateBasePackages = _("no");
    }

    string labelAutomaticUpdate = sformat ("%1", automatikUpdate);	// integer -> string
    string labelManuellUpdate =   sformat ("%1", manuellUpdate);	// integer -> string

    // Build and show dialog

    term contents = `HVSquash(
			      `VBox(`VBox(
					  // update dialogue, label for old version field
					  `Left(`Label( `id(`InstalledVersion), _("Installed version:") )),
					  `HWeight( 80,
						    `Left(`Label( `id(`labelInstalledVersion),
								  `opt(`outputField,`hstretch ),
								  labelInstalledVersion ))
						    ),
					  // update dialogue, label for new version field
					  `Left(`Label( `id(`UpdateVersion),_("Update version:") )),
					  `HWeight( 80,
						    `Left(`Label( `id(`labelUpdateVersion),
							`opt(`outputField,`hstretch),
								  labelUpdateVersion ))
						    ),
					  // update dialogue, yes/no label
					  `Left(`Label( `id(`UpdateBasePackages),_("Update base packages:") )),
					  `HWeight( 80,
						    `Left(`Label( `id(`labelUpdateBasePackages),
								  `opt(`outputField,`hstretch),
								  labelUpdateBasePackages ))
						    ),
					  // update dialogue, total count of affected packages
					  `Left(`Label( `id(`AutomaticUpdate),_("Packages to update:") )),
					  `HWeight( 80,
						    `Left(`Label( `id(`labelAutomaticUpdate),
								  `opt(`outputField,`hstretch),
								  labelAutomaticUpdate ))
						    ),
					  // update dialogue, number of packages to be installed but which
					  // are not the original ones (not from a SuSE system)
					  `Left(`Label( `id(`ManuellUpdate),_("Packages to check manually:") )),
					  `HWeight( 80,
						    `Left(`Label( `id(`labelManuellUpdate),
								  `opt(`outputField,`hstretch ),
								  labelManuellUpdate ))
						    )
					  ),
				    `VSpacing(),
				    // Push button that will pop up the detailed
				    // software selection
				    `PushButton( `id(`details),  _("&Detailed selection...")
						 )
				    )
			     );


    // explain choosable system configurations
    // but beware: some of the text in <b>'s, e.g. Default comes from the index file,
    // translations must be consistent with po/index/index.??.po
    // help part 1

    string helptext = _("<p>
The update process recognizes packages that must be updated.
For some packages, the user must decide (with the button \"Detailed selection\")
if the package should be updated.
</p>
");

    
    // Checking depencenies
    boolean ok = Pkg::PkgSolve();

    // FIXME
    if (!ok)
    {
	// There are unresolved package-dependencies while upgrade
	any ret = CallFunction( `inst_packages() );
	return `again;
    }

    // Checking disk-space
    SpaceCalculation::ShowPartitionWarning();
    
    Wizard::SetContents(title, contents, helptext, Args(0),Args(1));
  
    any ret = nil;
    boolean error_found = false;

    while (true)
    {
	ret = UI::UserInput();

	if (ret == `cancel || ret == `back ) break;

	if ( ret == `abort && CallFunction(`inst_confirm_abort(`painless) ) )
	    return `abort;

	if ( ret == `details )
	{
	    ret = CallFunction( `inst_packages ( ) );

	    if ( ret == `ok || ret == `cancel_single )
		ret = `again;
	    break;

	}

	if ( ret == `next )
	{
	    if ( Update::affected_count <= 0 )
	    {
		UI::MessagePopup(_("There is nothing to update."));
		ret = `again;
		break;
	    }

	    if ( size ( Update::manuall_update_list ) > 0 )
	    {
		string message =
			_("There are packages that need a manual decision
whether to perform the update.
We recommend updating these packages as well,
but you should have a look at it.

Would you like to browse though these packages?
");

		if ( UI::YesNoPopup(message) )
		{
		    ret = CallFunction( `inst_packages () );

		    if ( ret == `ok || ret == `cancel_single )
			ret = `again;
		    break;
		}
	    }

	    // FIXME
	    if (false)
	    {
		UI::MessagePopup( _("Base packages can only be updated
if the system has been booted from CD.
Boot from CD.
") );
		ret = `again;
	    }
	    else
	    {
		break;
	    }
	}
    }

    y2debug( "inst_sw_update return: %1", ret );

    return ret;
}
