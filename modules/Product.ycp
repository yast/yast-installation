/**
 * File:	modules/Product.ycp
 * Package:	yast2
 * Summary:	Product data
 * Authors:	Klaus Kaempf <kkaempf@suse.de>
 *
 * $Id$
 */

{
    module "Product";

    import "Mode";

    /**
     * General product name and version
     */
    global string name		= "";			// "SuSE Linux"
    global string version	= "";			// "8.1"
    global string vendor	= "";			// "SuSE Linux AG"

    /**
     * Distribution: Personal, Professional, etc.
     */
    global string dist		= "";
    global string distproduct	= "";			// "SuSE-Linux-Professional-INT-i386"
    global string distversion	= "";			// "8.1-0"

    /**
     * base product
     */
    global string baseproduct	= "";			// "UnitedLinux"
    global string baseversion	= "";			// "1.0"

    /**
     * url of release notes (downloaded during internet test)
     */
    global string relnotesurl	= "";

    /**
     * true if IMAP server
     */
    global boolean imap_server	= false;

    /**
     * true if Open Team server
     */
    global boolean openteam_server	= false;

    /**
     * true if Firewall Admin
     */
    global boolean fwadmin_host	= false;

    /**
     * true if SLES server
     */
    global boolean sles_server	= false;

    /**
     * true if SLCS server
     */
    global boolean slcs_server	= false;

    /**
     * true for Certification CD
     */
    global boolean certify_cd	= false;

    /**
     * true for general product CD
     */
    global boolean product_cd	= false;

    /**
     * true for SuSE Linux Personal
     */
    global boolean personal	= false;

    /**
     * true for UnitedLinux
     */
    global boolean unitedlinux	= false;

    /**
     *  Control how the base selection is chosen:
     *    `fixed - the base selection is fixed (see fixed_base_selection below)
     *    `auto  - a base selection is automatically choosen (depending on disk
     *             space)
     */
    global symbol base_selection_choice		= `auto;

    /**
     *  Name of the fixed base selection.
     */
    global string fixed_base_selection		= "";

    /**
     *  If non-empty it will be called with WFM::CallFunction
     * 	from Packages::ComputeSystemPackageList. Gets a list of packages
     *  as the one and only argument and must return a list of packages
     *  again.
     */
    global string packages_transmogrify		= "";

    /**
     *  Run YOU during the Internet connection test.
     */
    global boolean run_you	= true;

    /**
     * list of flags from content file
     */
    global list flags		= [];

    // -----------------------------------------------
    /**
     * Constructor
     */
    global define void Product () ``{

	if (Mode::initial)
	{
	    name		= (string) SCR::Read(.content.PRODUCT);
	    version		= (string) SCR::Read(.content.VERSION);
	    vendor		= (string) SCR::Read(.content.VENDOR);

	    distproduct		= (string) SCR::Read(.content.DISTPRODUCT);
	    distversion		= (string) SCR::Read(.content.DISTVERSION);

	    baseproduct		= (string) SCR::Read(.content.BASEPRODUCT);
	    if (baseproduct == "") baseproduct = name;
	    baseversion		= (string) SCR::Read(.content.BASEVERSION);

	    relnotesurl		= (string) SCR::Read(.content.RELNOTESURL);

	    any tmp1 = SCR::Read (.content.FLAGS);
	    if (tmp1 != nil)
		flags		= splitstring ((string) tmp1, " ");
	}
	else if (!Mode::config)
	{
	    Pkg::TargetInit ("/", false);
	    list all_products = Pkg::TargetProducts();		// get all products

	    map last_product = all_products[0]:$[];		// get last installed product

	    name		= last_product["name"]:"";
	    version		= last_product["version"]:"";
	    vendor		= last_product["vendor"]:"";
	    distproduct		= last_product["distproduct"]:"";
	    distversion		= last_product["distversion"]:"";
	    baseproduct		= last_product["baseproduct"]:"";
	    if (baseproduct == "") baseproduct = name;
	    baseversion		= last_product["baseversion"]:"";

	    relnotesurl		= last_product["relnotesurl"]:"";

	    flags		= splitstring (last_product["flags"]:"", " ");
	}

	imap_server	= (name == "eMail Server");
	openteam_server	= (name == "Openexchange Server");
	sles_server	= (name == "SusE SLES");
	slcs_server	= (name == "SusE SLCS");
	fwadmin_host	= (name == "SuSE Firewall Adminhost");
	certify_cd	= (name == "Certification CD");

	if(distproduct == nil) distproduct = "";
	dist		= select (splitstring(distproduct, "-"), 2, "");

	personal	= (dist == "Personal");
	unitedlinux	= (name == "UnitedLinux");

	/* handle live cd and certification cd as product cd,
	 * will disable software selection dialog
	 */

	product_cd	= Mode::live_eval
			  || certify_cd
			  || (name == "Product CD");

	run_you		= ! contains (flags, "no_you");
    }

/* EOF */
}
