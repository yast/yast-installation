/**
 * Module:	UML.ycp
 *
 * Authors:	Arvin Schnell <arvin@suse.de>
 *
 * Purpose:	UML installation module
 *
 * $Id$
 */
{
    module "UML";

    import "Users";
    import "Report";

    textdomain "installation";

    global integer memory_size = 256;

    global integer number_of_cpus = 1;

    global list <integer> disks = [ 1024 ];

    global string username = "";

    global string homedir = "";


    global define string propose_username() ``{
	string userprefix = "uml";
	integer index = 0;
	// limit number of user names for trying
	integer maxindex = 100;
	string ret = nil;

	while (index < maxindex && ret == nil)
	{
	    string user = sformat("%1%2", userprefix, index);

	    // check whether user already exists
	    string error = Users::CheckUsername(user);

	    if (error == "")
	    {
		ret = user;
	    }
	    else
	    {
		y2error("Error: %1", error);
	    }

	    index = index + 1;
	}

	return ret;
    }

    global define string propose_homedir(string user_name) ``{
	return (user_name != nil) ? (Users::GetDefaultHome("local") + user_name) : nil;
    }


    /**
     * Create specified UML user account in the system.
     * @return boolean true on success
     */
    global define boolean CreateUser() ``{
	map<string,any> user = $[
	    "homeDirectory" : homedir,
	    "userPassword"  : "",
	    "create_home"   : true,
	    "loginShell"    : "/bin/false",
	    "username"      : username,
	    "type"          : "local"
	];

	boolean ret = false;

	y2debug("Creating user: %1", user);


	Users::ResetCurrentUser ();

	if (Users::AddUser(user) == true)
	{
	    Users::CommitUser();

	    // don't show progress
	    Users::SetGUI(false);
	    ret = Users::Write();
	}
	else
	{
	    Report::Error(sformat(_("Cannot create account for user %1"), username));
	}

	return ret;
    }

    global void UserInitialize() ``{
	// initialize Users:: module
	Users::SetGUI(false);
	Users::Read();

	// propose user name and home directory
	username = propose_username();
	homedir = propose_homedir(username);
    }


    global void UML() ``{
	UserInitialize();
    }
}
