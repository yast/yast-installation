/**
 * File:	clients/inst_system_analysis.ycp
 * Package:	Installation
 * Summary:	Installation mode selection, system analysis
 * Authors:	Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 *
 */

{

textdomain "installation";

import "GetInstArgs";
import "Installation";
import "Kernel";
import "Label";
import "Linuxrc";
import "Mode";
import "Packages";
import "Popup";
import "Product";
import "ProductControl";
import "RootPart";
import "Stage";
import "Storage";
import "Update";
import "Wizard";

include "installation/misc.ycp";

string start_mode = Mode::mode ();


// FIXME copy-paste from inst_mode
    define symbol handle_boot ()
    {
	y2milestone( "User wishes to boot installed system" );

	if (!RootPart::didSearchForRootPartitions)
	{
	    UI::OpenDialog (`opt(`decorated ),
			    // label
			    `Label(_("Evaluating root partition. One moment please...")));
	    RootPart::FindRootPartitions ();
	    UI::CloseDialog ();
	}

	if (RootPart::numberOfValidRootPartitions == 0)
	{
	    // a popup
	    Popup::Message(_("No Linux root partition found."));
	    return `cancel;
	}

	// We must only change RootPart::selectedRootPartition if booting
	// is inevitable.

	if (RootPart::numberOfValidRootPartitions == 1)
	{
	    RootPart::SetSelectedToValid ();
	}
	else
	{
	    if (WFM::CallFunction ("inst_rootpart", [true, true, `boot_popup]) != `next)
		return `cancel;
	}

	string root_part = RootPart::selectedRootPartition;

	UI::OpenDialog( `opt(`decorated),
			// Information popup: Inform user what partition
			// will be booted from after user selected
			// "Boot installed system"
			`Label( sformat(_("Booting from %1"), root_part ) ) );

	Update::Detach ();
	RootPart::UnmountPartitions (false);

	sleep (1000);

	if ( ! Mode::test () )
	{
	    Linuxrc::WriteYaSTInf ($["Root" : root_part]);
	    Storage::ActivateHld(true);
	}

	UI::CloseDialog ();

	return `finish;
    }

term radio_box = `RadioButtonGroup (
    `id (`inst_mode),
    `MarginBox (
	`leftMargin (1),
	`rightMargin (1.5),
	`topMargin (0.5),
	`bottomMargin (0.5),
	`VBox(
	    // frame
	    `Frame (_("Installation Mode"), `VBox (
		// radio button
		`Left (`RadioButton (`id (`install), _("New &Installation"),
		    ! Mode::update ())),
		`VSpacing( 0.5 ),
		// radio button
		`Left (`RadioButton (`id (`update), _("&Update"),
		    Mode::update ()))
	    )),
	    `VSpacing (2),
	    // check box
	    `Left (`CheckBox (`id (`add_on), _("In&clude Add-On Products from Separate Media")))
	)
    )
);

term contents = `VBox (
    `VWeight (60, `HVCenter (radio_box)),
    `VWeight (10, `Empty ()),
    `HBox (
	`HWeight (4, `Empty()),
	// push button
	`PushButton (`id (`expert), _("&Other...")),
	`HWeight (1, `Empty ())
    ),
    `VWeight (10, `Empty ())
);

term expert_dialog = `VBox (
    // frame
    `MarginBox (2, 0.7, `Frame (_("Other Options"),
	`MarginBox (1, 0.5, `RadioButtonGroup (`id (`expert_mode), `VBox (
	    `Left (`RadioButton (`id (`repair),
		// radio button
		_("&Repair Installed System"), true)),
	    `Left (`RadioButton (`id (`boot),
		// radio button
		_("&Boot Installed System")))
	)))
    )),
    `Right (`HBox (
	`PushButton (`id (`ok), `opt (`default), Label::OKButton ()),
	`PushButton (`id (`cancel), Label::CancelButton ())
    ))
);

// help text for installation method
string help_text = _("<p><big><b>Installation Mode</b></big><br>
Select what to do:</p>");

// help text for installation method
help_text = help_text + _("<p>
Select <b>New Installation</b> if there is no existing Linux system on your
machine or if you want to replace an existing Linux system completely,
abandoning all its configuration data.
</p>");


// help text for installation method
help_text = help_text + _("<p>
Select <b>Update an Existing System</b> to update a Linux system already
installed on your machine. This option preserves configuration settings
from your existing system whenever possible.
</p>");

// help text for installation method
help_text = help_text + _("<p>
To install an Add-On Product from separate media together with &product;, select
<b>Include Add-On Products from Separate Media</b>.</p>");

// help text for installation method
help_text = help_text + _("<p>Click <b>Other</b> then 
select <b>Repair Installed System</b> if you have a damaged Linux system on
your hard disk. With this option, try to fix problems automatically.
</p>");

// help text for installation method
help_text = help_text + _("<p>Click <b>Other</b> then
select <b>Boot Installed System</b> if you have a Linux system on your hard
disk that will not boot. With this option, try to fix problems manually.
</p>");



Wizard::SetContents (_("Analyzing the Computer"), `Empty (), "", false, false);
Wizard::SetTitleIcon ("yast-inst-mode");

// Do hardware probing
//
// This must happen before submodule descriptions are initialized; module
// constructors might depend on it.
// In autoinst mode, this has been called already.

y2milestone ("Probing done: %1", Installation::probing_done);
if (! Installation::probing_done)
{
    if (!Mode::autoinst ())
    {
	WFM::CallFunction("inst_startup", []);
	WFM::CallFunction("inst_features", []);
	Installation::probing_done = true;
    }
}

// FIXME copy-paste from inst_mode
    boolean offer_update = false;
    y2milestone ("Product flags: %1", Product::flags);
    if (contains (Product::flags, "update") || Kernel::GetSuSEUpdate ())
    {
	offer_update = true;
    }

// FIXME copy-paste from inst_mode
    boolean have_linux = false;
    if (Storage::HaveLinuxPartitions ())
    {
	have_linux = true;
    }

y2milestone ("Have Linux: %1, Offer Update: %2", have_linux, offer_update);
if (! (have_linux && offer_update))
{
    Mode::SetMode ("installation");
    return `auto;
}
if (Mode::autoinst ())
    return `auto;

Wizard::SetContents (_("Installation Mode"), contents, help_text, true, true);
Wizard::SetTitleIcon ("yast-inst-mode");

symbol ret = nil;
symbol new_mode = Mode::update () ? `update : `install;

repeat {
    ret = (symbol)UI::UserInput();

    if (ret == `next)
    {
	new_mode = (symbol)UI::QueryWidget (`id (`inst_mode), `CurrentButton);
	if (new_mode == nil)
	{
	    // this is a label of a message box
	    Popup::Message(_("Choose one of the\noptions to continue."));
	    continue;
	}
    }
    else if (ret == `expert)
    {
	UI::OpenDialog( expert_dialog );
	ret = (symbol)UI::UserInput();
	if (ret == `ok)
	{
	    new_mode = (symbol)
		UI::QueryWidget (`id (`expert_mode), `CurrentButton);
	    if (new_mode == `boot)
	    {
		if (handle_boot () == `finish)
		    Mode::SetMode ("");
		else
		    ret = nil;
	    }
	    ret = `next;
	}
	UI::CloseDialog();
    }
    else if (ret == `abort)
    {
	if (Popup::ConfirmAbort (Stage::initial () ? `painless : `incomplete))
	    return `abort;
	continue;
    }
} until (ret == `back || ret == `next);


if (ret == `next)
{
    if (new_mode == `install)
    {
	Mode::SetMode ("installation");
    }

    if (new_mode == `update)
    {
	Mode::SetMode ("update");
    }

    if (new_mode == `repair)
    {
	Mode::SetMode ("repair");
	ret = `finish;
    }

    if (new_mode == `boot)
    {
	Mode::SetMode ("");
	ret = `finish;
    }

    if (new_mode == `install || new_mode == `update)
    {
	boolean add_on = (boolean)UI::QueryWidget (`id (`add_on), `Value);
	ProductControl::DisabledModules = filter (string m, ProductControl::DisabledModules, {
	    return m != "add-on" && m != "inst_add-on";
	});
	if (! add_on)
	{
	    ProductControl::DisabledModules = add (ProductControl::DisabledModules, "add-on");
	    ProductControl::DisabledModules = add (ProductControl::DisabledModules, "inst_add-on");
	}
    }
}


if (ret != `finish && Mode::mode () != start_mode)
{
    y2milestone ("Switching Steps from %1 to %2 ", start_mode, Mode::mode ());
    UpdateWizardSteps ();
    y2milestone ("Resetting disk target to read values");
    Storage::ResetOndiskTarget ();
    y2debug ("Original target map (from disk): %1", Storage::GetTargetMap ());
    y2milestone ("Resetting package manager");
    Kernel::ProbeKernel();
    Pkg::TargetFinish ();
    Pkg::PkgReset ();
    Packages::Init( true );

    if (! Mode::repair ())
    {
	ret = ProductControl::RunFrom (
	    ProductControl::CurrentStep () + 1,
	    true);
	if (ret == `next)
	    ret = `finish;
    }
}
else if (ret != `finish && ! Mode::repair())
{
    UpdateWizardSteps ();
    ret = ProductControl::RunFrom (
	ProductControl::CurrentStep () + 1,
	true);
    if (ret == `next)
	ret = `finish;

}

if (Mode::repair ())
{
    WFM::CallFunction ("inst_repair");
}

return ret;

/* EOF */
}
