/**
 * File:	clients/automatic_configuration_finish.ycp
 * Package:	installation
 * Summary:	Automatic configuration instead of the second stage.
 *		DO NOT USE THIS CLIENT ANYMORE.
 * Authors:	Lukas Ocilka <locilka@suse.cz>
 * ThanksTo:	Martin Vidner <mvidner@suse.cz>
 *		Ladislav Slezak <lslezak@suse.cz>
 *
 * $Id:$
 *
 * @see http://visnov.blogspot.com/2008/02/getting-rid-of-2nd-stage-of.html
 */

{
    import "Report";
    import "FileUtils";
    import "Installation";
    import "ProductControl";

    textdomain "installation";

    y2error ("DO NOT USE THIS CLIENT ANYMORE!");
    return `auto;

    any ret = nil;
    string func = "";
    map param = $[];

    /* Check arguments */
    if(size(WFM::Args()) > 0 && is (WFM::Args (0), string)) {
	func = (string) WFM::Args (0);
	if (size (WFM::Args()) > 1 && is (WFM::Args (1), map))
	    param = (map) WFM::Args (1);
    }
    y2milestone ("starting automatic_configuration_finish");
    y2debug("func=%1", func);
    y2debug("param=%1", param);

    // functions -->

    string zypp_lock_file = "/var/run/zypp.pid";

    void RemoveZyppLockIfExists () {
	// a bit cruel, remove the zypp log if exists
	if (FileUtils::Exists (zypp_lock_file)) {
	    y2milestone ("Removing zypp lock %1", zypp_lock_file);
	    SCR::Execute (.target.remove, zypp_lock_file);
	}
    }

    void Write () {
	// Just to be sure...
	Pkg::SourceFinishAll();
	Pkg::TargetFinish();

	RemoveZyppLockIfExists();
	// FIXME: should be done somehow else and somewhere else...?
	if (! FileUtils::Exists ("/mnt/etc/install.inf")) {
	    y2milestone ("Copying install.inf");
	    WFM::Execute (.local.bash, "cp /etc/install.inf /mnt/etc/install.inf");
	}

	y2milestone ("~~~ MAGIC! -----------------------------------------------------------------");

	integer old_SCR = WFM::SCRGetDefault();
	integer new_SCR = WFM::SCROpen ("chroot="+Installation::destdir+":scr", false);
	WFM::SCRSetDefault (new_SCR);

	RemoveZyppLockIfExists();
	y2milestone ("Running autoconf - a separate YaST process");
	// FIXME: solve Y2DEBUG somehow
	string cmd = "/usr/lib/YaST2/bin/y2base /usr/share/YaST2/clients/inst_automatic_configuration.ycp UI";
	// FIXME: use process agent
	//        and use lome logging to file and `tail -F` from here
	map cmd_ret = (map) SCR::Execute (.target.bash_output, cmd);
	y2milestone ("Command '%1' returned %2", cmd, cmd_ret);

	WFM::SCRClose (new_SCR);
	WFM::SCRSetDefault (old_SCR);

	y2milestone ("~~~ MAGIC! -----------------------------------------------------------------");
    }

    // <-- functions

    // main()
    if (func == "Info") {
	ret = (any) $[
	    "steps" : 1,
	    // progress step title
	    "title" : _("Writing automatic configuration..."),
	    // only when requested
	    "when" : (ProductControl::GetUseAutomaticConfiguration() == true ? [ `installation ] : []),
	];
    } else if (func == "Write") {
	Write();
    } else {
	y2error ("unknown function: %1", func);
	ret = nil;
    }

    return ret;
}
