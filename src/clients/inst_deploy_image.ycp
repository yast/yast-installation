{
import "Installation";
import "ImageInstallation";
import "Progress";
import "Wizard";
import "SourceManager";

textdomain "installation";

// There is nothing to do
if (! Installation::image_installation) {
    y2milestone ("No images have been selected");
    return `auto;
}

y2milestone ("Deploying image");

list<string> images = ImageInstallation::ImageOrder ();

integer last_image = nil;

/**
 * Set the progress
 * @param image integer the number of image
 * @param percent integer the percentage of the particular image
 */
void SetProgress (integer image, integer percent) {
    if (percent >= 100)
	image = image + 1;

    if (image != last_image) {
	last_image = image;
	if (image < size (images)) {
	    UI::ChangeWidget (
		`id ("deploying_progress"),
		`Label,
		sformat (_("Deploying Image (%1/%2)..."), (image + 1), size (images))
	    );
	}
    }

    UI::ChangeWidget (
	`id ("deploying_progress"),
	`Value,
	image
    );
}

ImageInstallation::StoreAllChanges();

Pkg::TargetFinish ();

Wizard::SetContents (
    _("Deploying Installation Images"),
    `ProgressBar (
	`id ("deploying_progress"),
	_("Deploying Images..."),
	size(images),
	0
    ),
    "",
    false, false
);
Wizard::SetTitleIcon ("yast-inst-mode");

SourceManager::InstInitSourceMoveDownloadArea();
ImageInstallation::DeployImages (images, Installation::destdir, SetProgress);

y2milestone ("Target image for package selector prepared");

Pkg::TargetInitialize (Installation::destdir);
Pkg::TargetLoad ();

ImageInstallation::RestoreAllChanges();

return `next;

}
