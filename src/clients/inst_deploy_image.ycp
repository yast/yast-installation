{
import "Installation";
import "ImageInstallation";
import "Progress";
import "Wizard";
import "SourceManager";
import "String";

textdomain "installation";

// There is nothing to do
if (! Installation::image_installation) {
    y2milestone ("No images have been selected");
    return `auto;
}

y2milestone ("Deploying image");

list<string> images = ImageInstallation::ImageOrder ();

integer last_image = nil;

/**
 * Set the progress
 * @param image integer the number of image
 * @param percent integer the percentage of the particular image
 */
void SetProgress (integer image, integer percent) {
    if (percent >= 100)
	image = image + 1;

    if (image != last_image) {
	last_image = image;
	if (image < size (images)) {
	    UI::ChangeWidget (
		`id ("deploying_progress"),
		`Label,
		sformat (_("Deploying Images (%1/%2)..."), (image + 1), size (images))
	    );
	}
    }

    UI::ChangeWidget (
	`id ("deploying_progress"),
	`Value,
	image
    );
}

integer _last_download_progress = -1;

boolean MyProgressDownloadHandler (integer percent, integer bps_avg, integer bps_current) {
    // FIXME: remove this line
    y2milestone ("PerCent: %1, AVG: %2, Spd: %3", percent, bps_avg, bps_current);

    if (_last_download_progress < percent) {
	UI::ChangeWidget (
	    `id ("one_image"), `Label,
	    sformat (_("Downloading image at speed %1/s"), String::FormatSize (bps_current))
	);
	UI::ChangeWidget (`id ("one_image"), `Value, percent);
    }

    _last_download_progress = percent;

    return true;
}

integer _last_progress = -1;
string _last_image_id = nil;

void SetOneImageProgress (integer current_progress) {
    map <string, any> current_image = ImageInstallation::GetCurrentImageDetails();
    integer max_progress = current_image["max_progress"]:0;

    // another file than the previous one
    if (current_image["file"]:"" != _last_image_id) {
	_last_image_id = current_image["file"]:"";
	_last_download_progress = -1;
	_last_progress = -1;
    }

    if (max_progress == nil || max_progress == 0) {
	y2milestone ("Can't find max_progress: %1", current_image);
	return;
    }

    // current progress 0 - 100
    integer x_progress = (100 * current_progress / max_progress);
    if (x_progress > 100) x_progress = 100;

    // reset the label
    if (x_progress == 0) {
	UI::ChangeWidget (`id ("one_image"), `Label, _("Deploying image..."));
    // one image done
    } else if (x_progress == 100) {
	UI::ChangeWidget (`id ("one_image"), `Label, _("Image deployed"));
    }

    // set current step
    if (x_progress > _last_progress) {
	 UI::ChangeWidget (`id ("one_image"), `Value, x_progress);
	_last_progress = x_progress;
    }
}

ImageInstallation::SetDeployTarImageProgress (SetOneImageProgress);
ImageInstallation::SetDownloadTarImageProgress (MyProgressDownloadHandler);

ImageInstallation::StoreAllChanges();

Pkg::TargetFinish ();

Wizard::SetContents (
    _("Deploying Installation Images"),
    `VBox (
	`ProgressBar (
	    `id ("one_image"),
	    _("Deploying image..."),
	    100,
	    0
	),
	`ProgressBar (
	    `id ("deploying_progress"),
	    _("Deploying Images..."),
	    size(images),
	    0
	)
    ),
    "",
    false, false
);
Wizard::SetTitleIcon ("yast-inst-mode");

SourceManager::InstInitSourceMoveDownloadArea();

ImageInstallation::DeployImages (images, Installation::destdir, SetProgress);

y2milestone ("Target image for package selector prepared");

Pkg::TargetInitialize (Installation::destdir);
Pkg::TargetLoad ();

ImageInstallation::RestoreAllChanges();

return `next;

}
