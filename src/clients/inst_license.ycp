/**
 * File:	clients/inst_license.ycp
 * Package:	Installation
 * Summary:	Generic License File
 * Authors:	Anas Nashif <nashif@suse.de>
 *		Jiri Srain <jsrain@suse.cz>
 *		Lukas Ocilka <locilka@suse.cz>
 *
 * $Id$
 *
 */

{

textdomain "installation";

import "Directory";
import "GetInstArgs";
import "Stage";
import "ProductLicense";
import "Mode";

// all the arguments
map argmap = GetInstArgs::argmap();

// Action if license is not accepted
// abort|continue|halt
// halt is the default
// bugzilla #252132
string action = argmap["action"]:"halt";

// Do not halt the machine in case of declining the license
// just abort
// bugzilla #330730
if (Mode::live_installation()) {
    action = "abort";
}

symbol ask_ret = nil;

if (Stage::initial ())
{
    ask_ret = ProductLicense::AskFirstStageLicenseAgreement (action);
}
else
{
    string directory = argmap["directory"]:Directory::etcdir + "/license/";
    if (argmap["directory"]:"" != "")
	directory = Directory::custom_workflow_dir + directory;

    // patterns are hardcoded
    ask_ret = ProductLicense::AskInstalledLicenseAgreement (directory, [], action);
}

if (ask_ret == nil || ask_ret == `auto)
    return `auto;
else if (ask_ret == `abort || ask_ret == `back)
    return ask_ret;
else if (ask_ret == `halt)
{
    UI::CloseDialog();
    // License has been aborted
    // bugzilla #282958
    SCR::Execute(.target.bash, "/sbin/halt -f -n -p");
    return `abort;
}
else if (ask_ret == `next)
    return `next;
else if (ask_ret == `accepted)
    return `next;
else
{
    y2error ("Unknown return: %1", ask_ret);
    return `next;
}

/* EOF */
}
