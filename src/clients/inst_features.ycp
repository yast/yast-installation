/**
 * Module:	inst_features.ycp
 *
 * Authors:	Anas Nashif <nashif@suse.de>
 *
 * Purpose:     Enable all the set features in the control file
                before going into proposal
 * $Id$
 */
{
    textdomain "installation";

    import "ProductControl";
    import "ProductFeatures";
    import "Timezone";
    import "Keyboard";
    import "Language";
    import "Installation";
    import "Console";
    import "RunlevelEd";

    // Timezone
    if (ProductFeatures::GetStringFeature ("globals", "timezone") != "")
    {
        Timezone::Set( ProductFeatures::GetStringFeature ("globals", "timezone"), true );
    }

    // Keyboard
    if (ProductFeatures::GetStringFeature ("globals", "keyboard") != "")
    {
        Keyboard::default_kbd = ProductFeatures::GetStringFeature ("globals", "keyboard");
        Keyboard::SetConsole( ProductFeatures::GetStringFeature ("globals", "keyboard"));
        Keyboard::SetX11( ProductFeatures::GetStringFeature ("globals", "keyboard") );
    }

    // Runlevel
    if (ProductFeatures::GetStringFeature ("globals", "runlevel") != "")
    {
        RunlevelEd::default_runlevel = ProductFeatures::GetStringFeature ("globals", "runlevel");
    }

    if (ProductFeatures::GetStringFeature ("globals", "language") != "")
    {
        string language = ProductFeatures::GetStringFeature ("globals", "language");
        // Set it in the Language module.
        Language::Set( language );

        // Set Console font
        Console::SelectFont( language );

        // Set it in YaST2
        Language::WfmSetLanguage();
    }

    // Bugzilla #327791
    // Online Repositories - default status

    // Feature is not defined
    if (ProductFeatures::GetFeature ("software", "online_repos_preselected") == "") {
	// Default is true - selected
	Installation::productsources_selected = true;
    // Defined
    } else {
	boolean default_status_or = ProductFeatures::GetBooleanFeature ("software", "online_repos_preselected");

	// if not set, default is "true"
	if (default_status_or == nil) default_status_or = true;
	Installation::productsources_selected = default_status_or;
    }

    y2milestone ("Use Online Repositories (default): %1", Installation::productsources_selected);


    return `auto;
}
